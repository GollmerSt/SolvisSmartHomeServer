; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyBaseDir       "..\..\publish Linux"
#define MyAppName       "SolvisSmartHomeServer"
#define MyInstallerName "SolvisSmartHomeServer setup"
#define MyAppVersion    "1.1.0"
#define MyAppExeName    "Startup.bat"
#define MyJarFile       "SolvisSmartHomeServer.jar"
#define MyIconName      "Solvis.ico"
#define MyWindowsDir    "..\..\SmartHome\Windows"


[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{CCDE2384-401A-40B6-B757-5627A2072B79}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName=C:\JavaPgms\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
InfoAfterFile={#MyBaseDir}\CHANGES.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=admin
;PrivilegesRequiredOverridesAllowed=dialog
OutputBaseFilename={#MyInstallerName}
OutputDir=..\..\publish Windows
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64
RestartIfNeededByRun=no

;[Languages]
;Name: "english"; MessagesFile: "compiler:Default.isl"
;Name: "german"; MessagesFile: "compiler:Languages\German.isl"


[Files]
Source: "Startup.bat"; DestDir: "{app}"; Flags: ignoreversion; 
Source: "{#MyBaseDir}\base.xml.new"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#MyBaseDir}\base.xsd"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#MyBaseDir}\CHANGES.txt"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#MyBaseDir}\SolvisSmartHomeServer.jar"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#MyWindowsDir}\{#MyIconName}"; DestDir: "{app}"; Flags: ignoreversion;
; NOTE: Don't use "Flags: gnoreversion" on any shared system files

[Tasks]
Name: "run"; Description: "Automatic start";
Name: "run\OnWindowsStart"; Description: "Start with windows"; Flags: exclusive;
Name: "run\OnLogon"; Description: "Start on logon"; Flags: exclusive unchecked;


[Icons]
Name: "{group}\{#MyAppName}"; Filename: "javaw";  Parameters: "-jar ""{app}\{#MyJarFile}"""; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\{#MyAppName} in DOS window"; Filename: "{app}\{#MyAppExeName}"; Parameters: "dos"; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\Learn"; Filename: "{app}\{#MyAppExeName}"; Parameters: "learn"; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\Terminate"; Filename: "javaw";  Parameters: "-jar ""{app}\{#MyJarFile}"" --server-terminate"; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\CreateCsv"; Filename: "{app}\Startup.bat";  Parameters: "csv"; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\Crypt"; Filename: "{app}\{#MyAppExeName}"; Parameters: "crypt"; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\Send testmail"; Filename: "javaw";  Parameters: "-jar ""{app}\{#MyJarFile}"" --test-mail"; IconFilename: "{app}\{#MyIconName}";
Name: "{group}\Uninstall {#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "java"; Description: "Create schedule task"; Parameters: "-jar ""{app}\{#MyJarFile}"" --create-task-xml=""{app}\Task.xml"" --onBoot"; WorkingDir: "{app}"; Tasks: run\OnWindowsStart;
Filename: "java"; Description: "Create schedule task"; Parameters: "-jar ""{app}\{#MyJarFile}"" --create-task-xml=""{app}\Task.xml"""; WorkingDir: "{app}"; Tasks: run\OnLogon;
Filename: "schtasks"; Description: "Schedule on booting"; Parameters: "/create /xml ""{app}\Task.xml"" /tn SolvisSmartHomeServerTask /f"; Tasks: run; 
;Filename: "schtasks"; Description: "Schedule on booting"; Parameters: "/create /sc ONLOGON /tn SolvisSmartHomeServerTask /tr ""{app}\Startup.bat"" /f"; Tasks: runOnWindowsStart
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

[UninstallRun]
Filename: "schtasks"; Parameters: "/delete /tn SolvisSmartHomeServerTask /f"; RunOnceId: "SolvisScheduler";

[UninstallDelete]
Type: files; Name: "{app}\Task.xml"